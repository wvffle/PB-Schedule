plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "net.wvffle.android.pb.schedule"
        minSdk 21
        targetSdk 31
        versionCode 4
        versionName "1.0.0-alpha.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    // Utils
    implementation 'commons-io:commons-io:2.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.preference:preference:1.1.1'

    // Components
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'com.balysv:material-ripple:1.0.2'
    implementation 'com.github.r3za13:android-timeline-schedule-view:0.9'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.0'
    implementation 'com.github.prolificinteractive:material-calendarview:2.0.0'
    implementation 'com.github.abdularis:circularimageview:1.5'
    implementation 'com.github.mancj:MaterialSearchBar:0.8.5'

    // Test and debugging
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'io.sentry:sentry-android:5.5.0'

    // APIs
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'joda-time:joda-time:2.9.4'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.android.gms:play-services-base:18.0.1'

    // Database
    debugImplementation 'io.objectbox:objectbox-android-objectbrowser:3.0.1'
    releaseImplementation 'io.objectbox:objectbox-android:3.0.1'
    annotationProcessor "io.objectbox:objectbox-processor:3.0.1"
}

apply plugin: 'io.objectbox'